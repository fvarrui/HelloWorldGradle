buildscript {
	repositories {
        mavenCentral()
	}
	dependencies {
		classpath 'io.github.fvarrui:javapackager:1.2.1-SNAPSHOT'
	}
}

plugins {
    id 'java'
    id 'maven-publish'
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

dependencies {
    implementation 'commons-io:commons-io:2.6'
}

group = 'io.github.fvarrui'
version = '1.0.0'
description = 'HelloWorld for Gradle'
sourceCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'

javapackager {
	mainClass = 'io.github.fvarrui.helloworld.Main'
	generateInstaller = true
	additionalResources = [ file('info.txt') ]
	bundleJre = true
}

task packageForWindows(type: io.github.fvarrui.javapackager.gradle.PackageTask, dependsOn: build) {
	description = 'Packages the application as a native Windows executable and bundles it in a zipball'
	platform = 'windows'
	winConfig {
		headerType = 'gui'
	}
	createZipball = true
}

task packageForLinux(type: io.github.fvarrui.javapackager.gradle.PackageTask, dependsOn: build) {
	description = 'Packages the application as a native GNU/Linux executable and bundles it in a zipball'
	platform = 'linux'
	jdkPath = file('C:/Program Files/AdoptOpenJDK/jdk-11.0.8+10-linux')
	createTarball = true
}

task packageForMac(type: io.github.fvarrui.javapackager.gradle.PackageTask, dependsOn: build) {
	description = 'Packages the application as a native Mac OS app and bundles it in a zipball'
	platform = 'mac'
	jdkPath = file('C:/Program Files/AdoptOpenJDK/jdk-11.0.8+10-mac/Contents/Home')
	createTarball = true
}

task packageForAllPlatforms(dependsOn: [ packageForWindows, packageForMac, packageForLinux ]) {
	description = 'Packages the application for all platforms'
	group = 'JavaPackager'
}

