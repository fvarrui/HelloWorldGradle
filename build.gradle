buildscript {
	repositories {
        mavenCentral()
	}
	dependencies {
		classpath 'io.github.fvarrui:javapackager:1.5.2-SNAPSHOT'
	}
}

plugins {
    id 'java'
    id 'maven-publish'
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

dependencies {
    implementation 'commons-io:commons-io:2.6'
}

group = 'io.github.fvarrui'
version = '1.0.0'
description = 'HelloWorld for Gradle'
sourceCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'

javapackager {
	mainClass = 'io.github.fvarrui.helloworld.Main'
	additionalResources = [ file('src/main/resources/info.txt') ]
	bundleJre = false
	generateInstaller = false
}

task packageForWindows(type: io.github.fvarrui.javapackager.gradle.PackageTask, dependsOn: build) {
	description = 'Packages the application as a native Windows executable and bundles it in a zipball'
	platform = 'windows'
	createZipball = false
}

task packageForLinux(type: io.github.fvarrui.javapackager.gradle.PackageTask, dependsOn: build) {
	description = 'Packages the application as a native GNU/Linux executable and bundles it in a tarball'
	platform = 'linux'
	createTarball = false
}

task packageForMac(type: io.github.fvarrui.javapackager.gradle.PackageTask, dependsOn: build) {
	description = 'Packages the application as a native Mac OS app and bundles it in a tarball'
	platform = 'mac'
	createTarball = false
}

task packageForAllPlatforms(dependsOn: [ packageForWindows, packageForMac, packageForLinux ]) {
	description = 'Packages the application for all platforms'
	group = 'JavaPackager'
}

